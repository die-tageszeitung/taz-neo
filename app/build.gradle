plugins {
    id "com.android.application"
    id "com.github.triplet.play" version "3.7.0"
    id "io.sentry.android.gradle" version "2.1.5"
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'


if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("nonfree")) {
    apply plugin: 'com.google.gms.google-services'
}

def keyStoreAvailable = false

file("../keystore.properties").with { propFile ->
    if (propFile.canRead()) {
        keyStoreAvailable = true
        println("Using keystore.properties file to sign APK")
    }
}

play {
    track.set('internal')
    serviceAccountCredentials.set(file('../play-publish.json'))
}


def versionNameToCode = { name ->
    def majorDigits = 2
    def minorDigits = 2
    def patchDigits = 2
    def preReleaseTypeDigits = 1
    def preReleaseVersionDigits = 2
    def versionParts = name.split('-')
    def semverCore = versionParts[0]
    def preReleaseIdentifier = null
    if (versionParts.length >= 2) {
        preReleaseIdentifier = versionParts[1]
    }
    def versionNumbers = []
    semverCore.split("\\.").eachWithIndex { item, idx ->
        versionNumbers.add(Integer.parseInt(item))
    }
    def (major, minor, patch) = versionNumbers
    // 9 is reserved for releases without pre-release identifier
    def preReleaseTypeNumber = 9
    def preReleaseVersion = 0
    if (preReleaseIdentifier != null) {
        def (preReleaseType, preReleaseVersionParsed) = preReleaseIdentifier.split('\\.')
        preReleaseVersionParsed = Integer.parseInt(preReleaseVersionParsed)
        preReleaseVersion = preReleaseVersionParsed
        switch (preReleaseType) {
            case "alpha":
                preReleaseTypeNumber = 1
                break
            case "beta":
                preReleaseTypeNumber = 2
                break
            case "rc":
                preReleaseTypeNumber = 3
                break
            default:
                throw Exception("Pre Release type need to be one of each 'alpha', 'beta' or 'rc'")
        }
    }
    return Integer.parseInt(
            major.toString().padLeft(majorDigits, '0') +
                    minor.toString().padLeft(minorDigits, '0') +
                    patch.toString().padLeft(patchDigits, '0') +
                    preReleaseTypeNumber.toString().padLeft(preReleaseTypeDigits, '0') +
                    preReleaseVersion.toString().padLeft(preReleaseVersionDigits, '0')
    )
}

def getVersionName = { ->
    def ENV = System.getenv()
    def ignoreDirty = ENV['VERSION_IGNORES_DIRTY_TREE']
    def proc
    if (ignoreDirty) {
        proc = 'git describe --tags'.execute()
    } else {
        proc = 'git describe --tags --dirty'.execute()
    }
    proc.waitFor()
    def versionName = proc.text.trim()
    println "This build has versionName: $versionName"
    return versionName
}

def getVersionCode = { ->
    try {
        def code = versionNameToCode(getVersionName())
        println "This build has versionCode: $code"
        return code
    } catch (e) {
        logger.warn(
                "Unable to determine versionCode from ${getVersionName()}, which is determined by the last git tag. " +
                        "Refer to the README to learn about a proper version format")
        e.printStackTrace()
        return 1
    }
}

android {
    compileSdkVersion 31

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "de.taz.app.android"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.

        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }

        vectorDrawables.useSupportLibrary = true
        resConfigs 'de'

        buildConfigField "Boolean", "MANUAL_UPDATE", "false"
        buildConfigField "Boolean", "IS_NON_FREE", "false"

        def ENV = System.getenv()
        def sentryEnvironment = ENV['SENTRY_ENVIRONMENT'] ?: 'local'
        println "Sentry environment: $sentryEnvironment"
        buildConfigField "String", "SENTRY_ENVIRONMENT", "\"${sentryEnvironment}\""
    }


    compileOptions {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi"
    }


    flavorDimensions "source", "owner", "graphql"

    productFlavors {
        taz {
            dimension "owner"
            applicationId "de.taz.android.app"
        }

        lmd {
            dimension "owner"
            applicationId "de.monde_diplomatique.android.app"
        }

        free {
            dimension "source"
            applicationIdSuffix ".free"
        }

        nonfree {
            dimension "source"
            buildConfigField "Boolean", "IS_NON_FREE", "true"
        }

        staging {
            applicationIdSuffix ".staging"
            dimension "graphql"
            buildConfigField "String", "GRAPHQL_ENDPOINT", "\"https://testdl.taz.de/appGraphQl\""
        }

        production {
            dimension "graphql"
            buildConfigField "String", "GRAPHQL_ENDPOINT", "\"https://dl.taz.de/appGraphQl\""
        }

    }

    signingConfigs {
        if (keyStoreAvailable) {
            keyStoreProperties {
                file("../keystore.properties").with { propFile ->
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))
                    storeFile file('../' + properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                }
            }
        }
    }

    buildTypes {

        debug {
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }

            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        leakCanary {
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }

            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig debug.signingConfig
            matchingFallbacks = ['debug', 'release']
        }
        unminifiedRelease {
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }
            minifyEnabled false
            matchingFallbacks = ['release']
        }
        manualUpdateRelease {
            buildConfigField "Boolean", "MANUAL_UPDATE", "true"
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }
            minifyEnabled false
            matchingFallbacks = ['release']
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    playConfigs {
        register("nonfreeTazProductionRelease") {

        }
    }

    packagingOptions {
        pickFirst "META-INF/AL2.0"
        pickFirst "META-INF/LGPL2.1"

        pickFirst 'win32-x86/attach_hotspot_windows.dll'
        pickFirst 'win32-x86-64/attach_hotspot_windows.dll'
        pickFirst 'META-INF/licenses/ASM'
    }
    lint {
        lintConfig file('lint.xml')
    }
}

dependencies {
    def coroutines_version = '1.5.2'
    def glide_version = '4.12.0'
    def ktor_version = '2.0.3'
    def lifecycle_version = '2.4.0'
    def mockito_version = '4.6.1'
    def powermock_version = '2.0.9'
    def room_version = '2.3.0'
    def work_version = '2.7.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.collection:collection-ktx:1.1.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation 'androidx.fragment:fragment-ktx:1.3.6'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.factor:bouncy:1.8'

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation 'androidx.autofill:autofill:1.1.0'

    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'androidx.browser:browser:1.4.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
    androidTestImplementation "io.ktor:ktor-client-mock:$ktor_version"

    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    implementation 'com.google.android.material:material:1.4.0'

    // coverflow
    implementation 'com.github.rubensousa:gravitysnaphelper:2.2.2'

    // stetho debug bridge
    implementation 'com.facebook.stetho:stetho:1.6.0'

    // pdf viewer
    implementation project(':mupdf-lib')

    // error reports
    implementation 'io.sentry:sentry-android:5.4.0'
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    // animations
    implementation 'com.airbnb.android:lottie:4.2.1'

    // iban validation
    implementation 'nl.garvelink.oss:iban:1.10.1'

    // testing
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "org.powermock:powermock-core:$powermock_version"
    testImplementation "org.powermock:powermock-module-junit4:$powermock_version"
    testImplementation "org.powermock:powermock-api-mockito2:$powermock_version"

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'


    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    def espressoVersion = '3.4.0'
    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espressoVersion"

    // Idling resources are also in production code
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"

    // room
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // leak canary
    leakCanaryImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // google Flavor
    nonfreeImplementation 'com.google.firebase:firebase-messaging:23.0.0'

    // add Java 8+ API desugaring support
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

sentry {
    // Enables or disables the automatic upload of mapping files
    // during a build.  If you disable this you'll need to manually
    // upload the mapping files with sentry-cli when you do a release.
    autoUpload = true
}


println "ATTENTION: Test build detected: kotlin classes annotated with @Mockable and its methods are opened"

apply plugin: 'kotlin-allopen'
allOpen {
    annotation('de.taz.app.android.annotation.Mockable')
}


task generateStaticVersionInBuildGradle {
    doLast {
        println "Injecting build static build version to enable mainfest parsing (for f-droid)"
        def scriptPath = buildscript.sourceFile.parentFile
        def versionCode = getVersionCode()
        def versionName = getVersionName()
        def injectVersions = "$scriptPath/inject_static_version.sh $versionName $versionCode $buildscript.sourceFile.path".execute()
        injectVersions.waitForProcessOutput(System.out, System.err)
    }
}
