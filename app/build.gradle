plugins {
    id 'com.github.triplet.play' version '2.6.2'
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'io.sentry.android.gradle'


if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("google")) {
    apply plugin: 'com.google.gms.google-services'
}

def keyStoreAvailable = false

file("../keystore.properties").with { propFile ->
    if (propFile.canRead()) {
        keyStoreAvailable = true
        println("Using keystore.properties file to sign APK")
    }
}

play {
    track = 'internal'
    serviceAccountCredentials = file('../play-publish.json')
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "de.taz.app.android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 23
        versionName "0.6"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        vectorDrawables.useSupportLibrary = true
        resConfig "de"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    flavorDimensions "store", "owner"

    productFlavors {
        taz {
            dimension "owner"
            applicationId "de.taz.android.app"
        }

        lmd {
            dimension "owner"
            applicationId "de.monde_diplomatique.android.app"
        }

        fdroid {
            dimension "store"
            applicationIdSuffix ".fdroid"
        }

        google {
            dimension "store"
        }

    }

    signingConfigs {
        if (keyStoreAvailable) {
            keyStoreProperties {
                file("../keystore.properties").with { propFile ->
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))
                    storeFile file('../' + properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                }
            }
        }
    }

    buildTypes {
        debug {
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }

            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            if (keyStoreAvailable) {
                signingConfig signingConfigs.keyStoreProperties
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    playConfigs {
        register("googleTazRelease") {

        }
    }
}

dependencies {
    def room_version = '2.2.5'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

    implementation 'androidx.appcompat:appcompat:1.2.0-alpha03'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'

    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'androidx.browser:browser:1.2.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    testImplementation "androidx.room:room-testing:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.work:work-runtime-ktx:2.3.4"

    implementation 'com.squareup.okhttp3:okhttp:4.2.0'
    implementation 'org.conscrypt:conscrypt-android:2.2.1'

    implementation 'com.squareup.moshi:moshi:1.8.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'

    implementation 'com.google.android.material:material:1.1.0'

    // coverflow
    implementation 'com.github.rubensousa:gravitysnaphelper:2.2.0'

    // stetho debug bridge
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // error reports
    implementation 'io.sentry:sentry-android:1.7.28'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    // testing
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.0'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    testImplementation 'org.powermock:powermock-core:2.0.4'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.4'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.4'

    // Core library
    androidTestImplementation 'androidx.test:core:1.2.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'

    // room
    androidTestImplementation 'androidx.room:room-testing:2.2.5'

    // google Flavor
    googleImplementation 'com.google.firebase:firebase-messaging:20.1.3'
}

// Open all classes and their methods annotated with @Mockable if this is a test build
def isTestTask = gradle.startParameter.taskNames.any { name -> name.split(':').last().startsWith 'test' }
def isAndroidStudioTestTask = gradle.startParameter.taskNames.any { name -> name.endsWith 'UnitTestSources' }
if (isTestTask || isAndroidStudioTestTask) {
    println "ATTENTION: Test build detected: kotlin classes annotated with @Mockable and its methods are opened"

    apply plugin: 'kotlin-allopen'
    allOpen {
        annotation('de.taz.app.android.annotation.Mockable')
    }
}
